;;;; game-of-life.lisp

(in-package #:game-of-life)

;; The start layout is hardcoded for now. It's a PD to be able to test the
;; various steps.
(defparameter *grid-layout* (list ".................."
                                  ".................."
                                  ".................."
                                  ".................."
                                  "......x....x......"
                                  "....xx.xxxx.xx...."
                                  "......x....x......"
                                  ".................."
                                  ".................."
                                  ".................."
                                  "..................")
  "The initial layout of the grid")

(defvar *grid* nil
  "The global variable that will hold the state of the grid.")

(defstruct cell
  "Represents a single cell."
  alive-p
  x
  y)

(defstruct grid
  "Represents the grid, with a flat list of cells."
  cells
  (width 0)
  (height 0))

(defun get-total-alive-neighbours (cell grid)
  "Counts the number of alive neighbours of a given cell."
  (let* ((x (cell-x cell))
         (y (cell-y cell))
         ;; A list of coordinates of the neighbouring cells.
         (neighbour-coords (list (list (1- x) (1- y)) ; top-left
                                 (list x (1- y))      ; top
                                 (list (1+ x) (1- y)) ; top-right
                                 (list (1+ x) y)      ; right
                                 (list (1+ x) (1+ y)) ; bottom-right
                                 (list x (1+ y))      ; bottom
                                 (list (1- x) (1+ y)) ; bottom-left
                                 (list (1- x) y)))    ; left
         ;; The actual neighbouring cells.
         (neighbours (mapcar
                      (lambda (pos)
                        (find-if (lambda (cell)
                                   (and
                                    (= (car pos) (cell-x cell))
                                    (= (cadr pos) (cell-y cell))))
                                 (grid-cells grid)))
                      neighbour-coords)))
    (length (remove-if-not #'cell-alive-p (remove nil neighbours)))))

(defun parse-grid-layout (layout)
  "Parses a grid layout into a list of cell structs. The grid layout should be a
   list of strings, with each string representing a single row of the grid."
  (loop for char across (format nil "窿灬秕舂骘骝镯泔祆邈戾舄è鏖漪戾铉翳ㄣ狎灬秕舂┅ㄡ扉鲥ㄣ栳蚪＼汨狎┅ㄦ祜矧ǒ鏖漪瑭┅盹鏖漪瑭┅磲脲沐祆横扉鲥犰轹壶葫┅┅ㄤ彐躅泸遽翦珧殇ㄧ蜷洵灬秕舂⒚蝈狒弩珧殇轭篝犷沐骝镯灬秕舢磲脲珧殇恒屐祗疳蝮瀛珧殇灬秕珧殇灬秕舂瑚殇翳戾铉翳ㄣ狎珧殇灬秕舂鸿彘玷戾铉翳珧殇灬秕舂┅ㄤ彐躅轭轸珧殇ī⑸铋糸犰辁弩翳珧殇箦翩珧殇ㄣ蝈狒瀛珧殇珧殇灬秕舄┅ㄤ彐躅珧殇篝屦ㄧ蜷洎⒂翦痼翳玑礤骘蝼狎镱趱蝾箦翩ㄧ蜷洵沐祆珧殇祜镳骘沐祆轭ㄧ蜷洵沐祆珧殇泔祆邈戾舄è犰轹瀛铄殓桠秕蝮ㄧ弭麸翎飙犰轹瀛铄殓桠秕蝮沐祆珧殇┅铄鳝篝狒矧犰轹瀛铄殓桠秕蝮畅ㄡ钿ㄣ屐飙犰轹瀛沐祆犰轹瀛铄殓桠秕蝮博┅┅磲脲沐祆横扉鲥铄鳝篝狒壶ㄣ屐飙沐祆葫ㄣ屐飙沐祆┅┅┅